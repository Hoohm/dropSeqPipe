"""Generate all the post alignement steps."""


# Configfile
configfile: 'config.yaml'

# Executables
TMPDIR = config['TMPDIR']
PICARD = config['PICARD']
DROPSEQ = config['DROPSEQ']
CORES = config['CORES']

# References files
GENOMEREF = config['GENOMEREF']
REFFLAT = config['REFFLAT']
RRNAINTERVALS = config['RRNAINTERVALS']

# Species
SPECIES = config["SPECIES"]


rule all:
	input: expand('logs/{sample}_hist_out_cell.txt', sample=config['Samples'])

rule sort:
	input:
		samples = '{sample}.Aligned.sam'
	output: temp('{sample}_Aligned_sorted.sam')
	threads: CORES
	shell:
		"""java	-Djava.io.tmpdir={TMPDIR} -Dsamjdk.buffer_size=131072 -XX:GCTimeLimit=50 -XX:GCHeapFreeLimit=10 -XX:ParallelGCThreads={CORES} -Xmx8g -Xms4096m -jar {PICARD} SortSam\
		INPUT={input}\
		OUTPUT={output}\
		SORT_ORDER=queryname\
		TMP_DIR={TMPDIR}"""

rule stage3:
	input:	unmapped = '{sample}_tagged_unmapped.bam',
			mapped = '{sample}_Aligned_sorted.sam'
	output: temp('{sample}_gene_exon_tagged.bam')
	threads: CORES
	shell:
		"""java -Djava.io.tmpdir={TMPDIR} -Xmx8g -Xms4096m -XX:ParallelGCThreads={CORES} -jar {PICARD} MergeBamAlignment\
		REFERENCE_SEQUENCE={GENOMEREF}\
		UNMAPPED_BAM={input.unmapped}\
		ALIGNED_BAM={input.mapped}\
		INCLUDE_SECONDARY_ALIGNMENTS=false\
		PAIRED_RUN=false\
		OUTPUT=/dev/stdout COMPRESSION_LEVEL=0|\

		{DROPSEQ}/TagReadWithGeneExon\
		OUTPUT={output}\
		INPUT=/dev/stdin\
		ANNOTATIONS_FILE={REFFLAT}\
		TAG=GE\
		CREATE_INDEX=true
		"""

rule bead_errors_metrics:
	input: '{sample}_gene_exon_tagged.bam'
	output: '{sample}_final.bam'
	threads: CORES
	params:
		out_stats = 'logs/{sample}_synthesis_stats.txt',
		summary = 'logs/{sample}_synthesis_stats_summary.txt',
		barcodes = lambda wildcards: config['Samples'][wildcards.sample]['expected_cells'] * 2,
		cells = lambda wildcards: config['Samples'][wildcards.sample]['expected_cells'],
		metrics = 'logs/{sample}_rna_metrics.txt',
		umis_per_cell = config['GLOBAL']['min_umis_per_cell']
	shell:
		"""{DROPSEQ}/DetectBeadSynthesisErrors\
		INPUT={input}\
		OUTPUT={output}\
		OUTPUT_STATS={params.out_stats}\
		SUMMARY={params.summary}\
		NUM_BARCODES={params.barcodes}\
		MIN_UMIS_PER_CELL={params.umis_per_cell}\
		PRIMER_SEQUENCE=AAGCAGTGGTATCAACGCAGAGTAC;
		{DROPSEQ}/SingleCellRnaSeqMetricsCollector\
		INPUT={input}\
		OUTPUT={params.metrics}\
		ANNOTATIONS_FILE={REFFLAT}\
		NUM_CORE_BARCODES={params.cells}\
		RIBOSOMAL_INTERVALS={RRNAINTERVALS}
		"""

rule bam_hist:
	input: '{sample}_final.bam'
	output: 'logs/{sample}_hist_out_cell.txt'
	shell:
		"""{DROPSEQ}/BAMTagHistogram\
		I={input}\
		O={output}\
		TAG=XC
		"""
